<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="UTF-8">
  <meta name="viewport" content="width=device-width, initial-scale=1.0">
  <title>glTF-PBR</title>

  <link rel="stylesheet" type="text/css" href="./main.css">
</head>
<body>
  <main>
    <canvas id="container"></canvas>

    <div id="control">
      <div class="item">
        <input type="radio" id="glTF" name="brdf" value="0" checked>
        <label for="glTF">glTF Implementation</label>
      </div>

      <div class="item">
        <input type="radio" id="Filament" name="brdf" value="1">
        <label for="Filament">Filament Standard Model</label>
      </div>

      <div class="item">
        <input type="radio" id="Microfacet" name="brdf" value="2">
        <label for="Microfacet">Microfacet Model</label>
      </div>
    </div>
  </main>

  <script id="vs" type="notjs">#version 300 es
    precision highp float;

    uniform mat4 u_modelMatrix;
    uniform mat4 u_viewMatrix;
    uniform mat4 u_projectionMatrix;
    uniform mat4 u_normalMatrix;

    in vec3 a_position;
    in vec3 a_normal;
    in vec2 a_texcoord;

    out vec2 v_texcoord;
    out vec3 v_normal;
    out vec3 v_position;

    void main() {
      gl_Position = u_projectionMatrix * u_viewMatrix * u_modelMatrix * vec4(a_position, 1.0);

      // Word space
      vec4 worldPosition = u_modelMatrix * vec4(a_position, 1.0);
      v_position = worldPosition.xyz / worldPosition.w;

      v_texcoord = a_texcoord;
      v_normal = normalize(mat3(u_normalMatrix) * a_normal);
    }
  </script>

  <script id="fs" type="notjs">#version 300 es
    precision highp float;

    const float PI = 3.14159265359;

    // 0: GLTF_BRDF, 1: Filament_BRDF, 2: Microfacet_BRDF
    uniform int u_brdf;

    // pbrMetallicRoughness
    uniform sampler2D u_baseColorTexture;
    uniform vec4 u_baseColorFactor;
    uniform sampler2D u_metallicRoughnessTexture;
    uniform float u_metallicFactor;
    uniform float u_roughnessFactor;

    uniform sampler2D u_normalTexture;
    uniform float u_normalScale;

    uniform sampler2D u_occlusionTexture;
    uniform float u_occlusionStrength;

    uniform sampler2D u_emissiveTexture;
    uniform vec3 u_emissiveFactor;

    uniform vec3 u_cameraPosition;
    uniform vec3 u_lightPosition;

    in vec3 v_position;
    in vec3 v_normal;
    in vec2 v_texcoord;

    out vec4 o_FragColor;


    mat3 TBN(vec3 v, vec2 vTextureCood, vec3 normal) {
      vec3  N       = normalize(normal);
      vec3  dp1     = dFdx(v);
      vec3  dp2     = dFdy(v);
      vec2  duv1    = dFdx(vTextureCood);
      vec2  duv2    = dFdy(vTextureCood);
      vec3  dp2perp = cross(dp2, N);
      vec3  dp1perp = cross(N, dp1);
      vec3  T       = dp2perp * duv1.x + dp1perp * duv2.x;
      vec3  B       = dp2perp * duv1.y + dp1perp * duv2.y;
      float invmax  = inversesqrt(max(dot(T, T), dot(B, B)));
      mat3  tm      = mat3(T * invmax, B * invmax, N);
      // mat3  tbn_inv = mat3(vec3(tm[0].x, tm[1].x, tm[2].x), vec3(tm[0].y, tm[1].y, tm[2].y), vec3(tm[0].z, tm[1].z, tm[2].z));

      return tm;
    }

    // Calculate GGX NDF
    float DistributionGGX(float NdotH, float alphaRoughness)
    {
      float a = NdotH * alphaRoughness;
      float k = alphaRoughness / (1.0 - NdotH * NdotH + a * a);
      return k * k * (1.0 / PI);
    }

    // Calculate Smith G1
    float GeometrySchlickGGX(float NdotV, float roughness)
    {
      float k = pow(roughness + 1.0, 2.0) / 8.0;
      float ggx = NdotV / (NdotV * (1.0 - k) + k);

      return ggx;
    }

    // Calculate Smith G
    float GeometrySmith(float NdotV, float NdotL, float roughness)
    {
      float nv_ggx = GeometrySchlickGGX(NdotV, roughness);
      float nl_ggx = GeometrySchlickGGX(NdotL, roughness);

      return nv_ggx * nl_ggx;
    }

    // Visibility term
    float V_SmithGGXCorrelated(float NdotV, float NdotL, float alphaRoughness) {
      float a2 = alphaRoughness * alphaRoughness;
      float GGXL = NdotV * sqrt((-NdotL * a2 + NdotL) * NdotL + a2);
      float GGXV = NdotL * sqrt((-NdotV * a2 + NdotV) * NdotV + a2);

      return 0.5 / (GGXV + GGXL);
    }


    // Calculate Schlick F
    vec3 FresnelSchlick(float HdotV, vec3 F0, vec3 F90)
    {
      return F0 + (F90 - F0) * pow(1.0 - HdotV, 5.0);
    }

    // https://github.com/KhronosGroup/glTF/blob/master/specification/2.0/README.md#appendix-b-brdf-implementation
    vec3 GLTF_BRDF(vec3 N, vec3 V, vec3 L, vec3 albedo)
    {
      vec3 H = normalize(V + L);
      float NdotV = max(dot(N, V), 0.0);
      float NdotL = max(dot(N, L), 0.0);
      float NdotH = max(dot(N, H), 0.0);
      float HdotV = max(dot(H, V), 0.0);

      vec4 mr = texture(u_metallicRoughnessTexture, v_texcoord);
      float roughness = mr.g * u_roughnessFactor;
      float metallic = mr.b * u_metallicFactor;
      float alphaRoughness = roughness * roughness;

      vec3 F90 = vec3(1.0);
      vec3 F0 = mix(vec3(0.04), albedo, metallic);

      float NDF = DistributionGGX(NdotH, alphaRoughness);
      float V_GGX = V_SmithGGXCorrelated(NdotV, NdotL, alphaRoughness);
      vec3 F = FresnelSchlick(HdotV, F0, F90);

      vec3 f_specular = F * NDF * V_GGX;

      vec3 c_diff = mix(albedo * (vec3(1.0) - vec3(0.04)), vec3(0.0), metallic);
      vec3 f_diffuse = (1.0 - F) * (c_diff / PI);

      return (f_diffuse + f_specular) * NdotL;
    }

    vec3 Filament_BRDF(vec3 N, vec3 V, vec3 L, vec3 albedo) {
      vec3 H = normalize(V + L);
      float NdotV = max(dot(N, V), 0.0);
      float NdotL = max(dot(N, L), 0.0);
      float NdotH = max(dot(N, H), 0.0);
      float HdotV = max(dot(H, V), 0.0);

      vec4 mr = texture(u_metallicRoughnessTexture, v_texcoord);
      float roughness = mr.g * u_roughnessFactor;
      float metallic = mr.b * u_metallicFactor;
      float alphaRoughness = roughness * roughness;

      vec3 F90 = vec3(1.0);
      vec3 F0 = mix(vec3(0.04), albedo, metallic);

      float NDF = DistributionGGX(NdotH, alphaRoughness);
      float V_GGX = V_SmithGGXCorrelated(NdotV, NdotL, alphaRoughness);
      vec3 F = FresnelSchlick(HdotV, F0, F90);

      // specular BRDF
      vec3 Fr = (NDF * V_GGX) * F;

      // diffuse BRDF
      vec3 diffuseColor = (1.0 - metallic) * albedo;
      vec3 Fd = diffuseColor / PI;

      return (Fr + Fd) * NdotL;
    }

    vec3 Microfacet_BRDF(vec3 N, vec3 V, vec3 L, vec3 albedo) {
      vec3 H = normalize(V + L);
      float NdotV = max(dot(N, V), 0.0);
      float NdotL = max(dot(N, L), 0.0);
      float NdotH = max(dot(N, H), 0.0);
      float HdotV = max(dot(H, V), 0.0);

      vec4 mr = texture(u_metallicRoughnessTexture, v_texcoord);
      float roughness = mr.g * u_roughnessFactor;
      float metallic = mr.b * u_metallicFactor;
      float alphaRoughness = roughness * roughness;

      vec3 F90 = vec3(1.0);
      vec3 F0 = mix(vec3(0.04), albedo, metallic);

      float NDF = DistributionGGX(NdotH, alphaRoughness);
      float G = GeometrySmith(NdotV, NdotL, roughness);
      vec3 F = FresnelSchlick(HdotV, F0, F90);

      vec3 BRDF = NDF * G * F / max((4.0 * NdotL * NdotV), 0.001);

      return BRDF * NdotL;
    }

    void main() {
      vec3 V = normalize(u_cameraPosition - v_position);
      vec3 L = normalize(u_lightPosition - v_position);
      vec3 perturbedNormal = texture(u_normalTexture, v_texcoord).rgb * 2.0 - 1.0;
      mat3 tbn = TBN(V, v_texcoord, normalize(v_normal));
      vec3 N = normalize(tbn * perturbedNormal);

      vec4 baseColor = texture(u_baseColorTexture, v_texcoord) * u_baseColorFactor;
      vec3 albedo = pow(baseColor.rgb, vec3(2.2));

      vec3 color = vec3(0.0);
      if (u_brdf == 0) {
        color = GLTF_BRDF(N, V, L, albedo);
      } else if (u_brdf == 1) {
        color = Filament_BRDF(N, V, L, albedo);
      } else if (u_brdf == 2) {
        color = Microfacet_BRDF(N, V, L, albedo);
      }

      // Occlusion
      float ao = texture(u_occlusionTexture, v_texcoord).r;
      color *= mix(1.0, ao, u_occlusionStrength);

      // Emissive
      vec3 emissive = texture(u_emissiveTexture, v_texcoord).rgb * u_emissiveFactor;
      color += emissive;

      // color = vec3(roughness);
      // color = vec3(metallic);
      // color = (N + 1.0) / 2.0;
      // color = vec3(NdotL);
      // color = f_diffuse;
      // color = f_specular;
      // color = albedo;

      color = pow(color, vec3(1.0/2.2));
      o_FragColor = vec4(color, baseColor.a);
    }
  </script>

  <script type="module">
    import { twgl, glMatrix, Camera, Control, loadersCore, loadersGLTF } from './lib/bundle.js';

    const { mat4, vec3, vec4 } = glMatrix;

    const canvas = document.getElementById('container');
    const gl = canvas.getContext("webgl2")
    const programInfo = twgl.createProgramInfo(gl, ['vs', 'fs'])

    const camera = new Camera({
      type: 'PerspectiveCamera',
      fov: 45,
      aspect: canvas.offsetWidth / canvas.offsetHeight,
      near: 0.01,
      far: 1000,
    });
    camera.setPosition(0, 0, 3);
    camera.lookAt(0, 0, 0);
    const control = new Control(camera, canvas, {
      minZoom: 0.6,
      maxZoom: 8,
      zoomScale: 0.01
    });
    control.start();

    function drawMesh(programInfo, mesh) {
      gl.useProgram(programInfo.program);

      gl.bindVertexArray(mesh.vao);
      twgl.setUniforms(programInfo, mesh.uniforms);
      twgl.drawBufferInfo(gl, mesh.bufferInfo);
    }

    function renderScene(scene) {
      twgl.resizeCanvasToDisplaySize(gl.canvas, 2.0);
      gl.enable(gl.DEPTH_TEST);
      gl.enable(gl.CULL_FACE);

      gl.clear(gl.COLOR_BUFFER_BIT | gl.DEPTH_BUFFER_BIT);
      gl.viewport(0, 0, gl.canvas.width, gl.canvas.height);

      // Default node mesh
      const node = scene.nodes[0]
      const mesh = node.mesh;
      const primitive = mesh.primitives[0];
      const geometry = {
        a_position: primitive.attributes.POSITION.value,
        a_normal: primitive.attributes.NORMAL.value,
        a_texcoord: primitive.attributes.TEXCOORD_0.value,

        indices: primitive.indices.value
      }
      mesh.bufferInfo = twgl.createBufferInfoFromArrays(gl, geometry);
      mesh.vao = twgl.createVAOFromBufferInfo(gl, programInfo,  mesh.bufferInfo);

      const { pbrMetallicRoughness, normalTexture, occlusionTexture, emissiveTexture, emissiveFactor } = primitive.material;

      // mat4.fromRotationTranslationScale
      const modelMatrix = mat4.fromQuat([], node.rotation);
      mesh.uniforms = {
        u_modelMatrix: modelMatrix,
        u_viewMatrix: camera.viewMatrix,
        u_projectionMatrix: camera.projectionMatrix,
        u_normalMatrix: mat4.transpose([], mat4.invert([], modelMatrix)),
        u_cameraPosition: camera.position,
        u_lightPosition: vec3.add([], camera.position, [0, 1, 0]),

        // PBR
        u_baseColorTexture: twgl.createTexture(gl, { src: pbrMetallicRoughness.baseColorTexture.texture.source.image }),
        u_baseColorFactor: pbrMetallicRoughness.baseColorFactor || [1.0, 1.0, 1.0, 1.0],
        u_metallicRoughnessTexture: twgl.createTexture(gl, { src: pbrMetallicRoughness.metallicRoughnessTexture.texture.source.image }),
        u_metallicFactor: pbrMetallicRoughness.metallicFactor || 1.0,
        u_roughnessFactor: pbrMetallicRoughness.roughnessFactor || 1.0,

        u_normalTexture: twgl.createTexture(gl, { src: normalTexture.texture.source.image }),
        u_normalScale: normalTexture.scale || 1.0,
        u_occlusionTexture: twgl.createTexture(gl, { src: occlusionTexture.texture.source.image }),
        u_occlusionStrength: occlusionTexture.strength || 1.0,
        u_emissiveTexture: twgl.createTexture(gl, { src: emissiveTexture.texture.source.image }),
        u_emissiveFactor: emissiveFactor || [1.0, 1.0, 1.0],
      };

      requestAnimationFrame(() => {
        anim(mesh)
      })
    }

    (async function() {
      const gltfUrl = './data/DamagedHelmet/glTF/DamagedHelmet.gltf';
      const gltf = await loadersCore.load(gltfUrl, loadersGLTF.GLTFLoader);

      renderScene(gltf.scenes[0]);
      initControl()
    })();

    let U_BRDF = 0;
    function initControl() {
      const controlEle = document.getElementById('control');
      controlEle.addEventListener('change', (e) => {
        U_BRDF = e.target.value;
      })
    }

    function anim(mesh) {

      mesh.uniforms = Object.assign(mesh.uniforms, {
        u_viewMatrix: camera.viewMatrix,
        u_cameraPosition: camera.position,
        u_lightPosition: vec3.add([], camera.position, [0, 1, 0]),

        u_brdf: U_BRDF,
      });

      drawMesh(programInfo, mesh);
      requestAnimationFrame(() => {
        anim(mesh);
      })
    }

  </script>
</body>
</html>